#!/usr/bin/expect -f

# The MIT License (MIT)
#
# Copyright (c) 2015 Denis Efremov
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.


#exp_internal 1

set force_conservative 0  ;# set to 1 to force conservative mode even if
			  ;# script wasn't run conservatively originally
if {$force_conservative} {
	set send_slow {1 .1}
	proc send {ignore arg} {
		sleep .1
		exp_send -s -- $arg
	}
}

proc yes {} {
   send -- "y\r"
   exp_continue
}

proc no {} {
   send -- "n\r"
   exp_continue
}

proc mod {} {
   send -- "m\r"
   exp_continue
}

proc def {} {
   send -- "\r"
   exp_continue
}


#proc quote {str} {
#   string map {
#      "("  "\\("
#      {)}  {\\)}
#      [  \\[
#      ]  \\]
#      {  \\{
#      }  \\}
#      "\?"  "\\?"
#      *  \\*
#      +  \\+
#   } $str
#}

proc quote {str} {
   return "\\(${str}\\)"
}

proc option {opt} {
   return "\\(${opt}\\)\\s+\\\[\[YyNnMm\\?\\/\]+\\\](\\s+\\(NEW\\))?\\s+$"
}

proc select {opt ynm} {
   return "-re {[option $opt]} { $ynm; }"
}

proc configuration {opts} {
   set str ""
   foreach {i j} [concat $opts] {
      append str [select $i $j] "\n"
   }
   uplevel 1 "
      expect {
         $str

         -re {\\\[\[\\w\\\/\\?\\(\\).\\-]*\\\]:?(\\s+\\(NEW\\))?\\s+$} {
            def
         }
         eof {
            exit
         }
         timeout {
            def
         }
      }"
}


spawn make config
#spawn ./test.sh
#sleep 5
match_max 100000

#expect -ex "scripts/kconfig/conf --oldaskconfig Kconfig" { sleep 1; }
expect -re {\* Linux\/x86 ([\d\.]+) Kernel Configuration} { sleep 1; }

set timeout 1

configuration {
   64BIT no

   GCOV_KERNEL      yes
   GCOV_PROFILE_ALL no

   REISERFS_FS             mod
      REISERFS_CHECK          yes
      REISERFS_FS_XATTR       yes
      REISERFS_FS_POSIX_ACL   yes
      REISERFS_FS_SECURITY    yes

   JFS_FS            mod
      JFS_POSIX_ACL     yes
      JFS_SECURITY      yes
      JFS_DEBUG         yes

   XFS_FS            mod
      XFS_QUOTA         yes
      XFS_POSIX_ACL     yes
      XFS_RT            yes
      XFS_WARN          yes
      XFS_DEBUG         yes


   MSDOS_FS    mod
   VFAT_FS     mod
   NTFS_FS     mod
      NTFS_RW     yes

   CONFIGFS_FS       mod

   MISC_FILESYSTEMS  yes
      MINIX_FS          mod
   
   DEBUG_KERNEL                  yes
   PANIC_ON_OOPS                 yes
   DEBUG_SPINLOCK                yes
   DEBUG_MUTEXES                 yes
   DEBUG_LOCK_ALLOC              yes
   DEBUG_ATOMIC_SLEEP            yes
   DEBUG_STACK_USAGE             yes
   DEBUG_WRITECOUNT              yes
   DEBUG_LIST                    yes
   DEBUG_SG                      yes
   FRAME_POINTER                 yes
   DEBUG_STRICT_USER_COPY_CHECKS yes
   DEBUG_PAGEALLOC               yes

   DEBUG_INFO                    yes
   KPROBES                       yes
   RELAY                         yes
   DEBUG_FS                      yes
   MODULES                       yes
      MODULE_UNLOAD                 yes

   PANIC_ON_OOPS                 yes
}

#   -re {(\[(([YyNnMm\?\/]+)|((0x)?\d+)|((\(none\))|(\/sbin\/hotplug)|(rtc0)|(utf8)|(iso8859-1)))?\]|choice\[1(-\d+)?\??\]:)(\s+\(NEW\))?\s*$} {
#      def
#   }

